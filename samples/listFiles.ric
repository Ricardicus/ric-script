#!/usr/bin/ric
# A script to demonstrate how one can list files using ric-script

@ printUsage() {
  print("usage: " + args[0] + " " + args[1] + " directory")
}

@ listFiles(folder, indent) {
  . ( folder.ls() ... file ) {
    fullfile = folder + "/" + file
    ? [ fullfile.isFile() ] {
      printf(" " * indent)
      print(file)
    } ~ {
      ? [ file != '.' && file != '..' ] {
        printf(" " * indent)
        print(file)
        fullfile.listFiles(indent + 1)
      }
    }
  }
}

# argument checking
? [ args.len() < 3 ] {
  printUsage()
  exit(1)
}

? [ isDir(args[2]) ] {
  # print folder structure
  listFiles(args[2], 0)
} ~ {
  # not a folder given
  printUsage()
  exit(1)
}

