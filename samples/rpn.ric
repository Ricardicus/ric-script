stack = {"head" : 0, "data" : []}
operators = ["+", "*", "-", "/"]
numberChars = ['0','1','2','3','4','5','6','7','8','9']

@Â isNumber(num) {
  isNr = 1
  i = 0
  . [ i < len(num) ] {
    ? [ contains( numberChars, num[i] ) ] {
      i = i + 1
      @
    } ~ {
      isNr = 0
    }
  }
  return isNr
}

@Â pop() {
  head = stack["head"]
  ? [ head == 0 ] {
    print("Too few arguments on the stack. Goodbye!")
    exit(2)
  }
  head = head - 1
  val = stack["data"][head]
  stack["head"] = head
  return val
}

@ push (val) {
  head = stack["head"]
  ? [ head >= len(stack["data"]) ] {
    append(stack["data"], val)
  } ~ {
    stack["data"][head] = val
  }
  stack["head"] = head + 1
}

@ eval (op) {
  ? [ op == '+' ] {
    return pop() + pop()
  } ~[ op == '-' ] {
    tmp = pop()
    return pop() - tmp
  } ~[ op == '*' ] {
    return pop() * pop()
  } ~[ op == '/' ] {
    tmp = pop()
    return pop() / tmp
  } ~ {
    return 0
  }
}

run = 1
. [ run ] {
  in = input(">> ")
  expr = split(in, " ")
  i = 0
  . [ i < len(expr) ] {
    ? [ isNumber(expr[i]) ] {
      push( parseInt(expr[i]) )
    } ~[ contains(operators, expr[i]) ] {
        s = eval(expr[i])
        push(s)
    } ~ {
      print("Sorry, I don't understad this: " + expr[i])
    }
    i = i + 1
    @
  }
  ? [ stack["head"] > 0 ] {
    print(stack["data"][ stack["head"] - 1 ])
  }
  stack["head"] = 0
  @
}

